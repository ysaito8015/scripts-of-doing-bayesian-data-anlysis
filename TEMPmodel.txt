
  data {
    int<lower=1> Ntotal ;
    real x[Ntotal] ;
    real y[Ntotal] ;
    real meanY ;
    real sdY ;
    real meanX ;
    real sdX ;
  }
  transformed data {
    real unifLo ;
    real unifHi ;
    real expLambda ;
    real beta0sigma ;
    real beta1sigma ;
    unifLo <- sdY/1000 ;
    unifHi <- sdY*1000 ;
    expLambda <- 1/30.0 ;
    beta1sigma <- 10*fabs(sdY/sdX) ;
    beta0sigma <- 10*fabs(meanX*sdY/sdX) ;
  }
  parameters {
    real beta0 ;
    real beta1 ;
    real<lower=0> nu ; 
    real<lower=0> sigma ; 
  }
  model {
    sigma ~ uniform( unifLo , unifHi ) ; 
    nu ~ exponential( expLambda ) ;
    beta0 ~ normal( 0 , beta0sigma ) ;
    beta1 ~ normal( 0 , beta1sigma ) ;
    for ( i in 1:Ntotal ) {
      y[i] ~ student_t( nu , beta0 + beta1 * x[i] , sigma  ) ;
    }
  }
  
